# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Peter Hatina <phatina AT gmail.com>
# Maintainer for SteamOS: Edmund Smith <ed.smith@collabora.com>
pkgname=lib32-glm
_base=glm
pkgver=1.0.0
pkgrel=1
pkgdesc="C++ mathematics library for 3D software based on the OpenGL Shading Language (GLSL) specification (32-bit)"
arch=(x86_64)
license=('MIT')
url="http://glm.g-truc.net"
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/g-truc/glm/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('62e22002a6369a54e1f0ee2885a65f2780af7d2a446573e5387b81518f5dc7e8076053837cb99ae850a0166ce8b0f077bed009e8986d9884d01c456ce467553f')

build() {
  cd ${_base}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  export CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -W no-dev
  cmake --build build --verbose
}

package() {
  cd ${_base}-${pkgver}

  DESTDIR="$pkgdir" cmake --install build

  rm -rf "$pkgdir"/usr/include
  mv "$pkgdir"/usr/share/glm "$pkgdir"/usr/lib32/glm

  install -vDm 644 copying.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
