# Maintainer: Ludovico de Nittis <ludovico.denittis@collabora.com>

pkgname=atomupd-daemon
pkgver=0.20260210.0
pkgrel=1
pkgdesc='Atomic updates daemon'
arch=('x86_64')
url='https://github.com/evlav/atomupd-daemon'
license=('MIT')
makedepends=('git' 'glib2-devel' 'meson' 'python-dbusmock'
  'python>=3.13'
  'python<3.14'
)
depends=('dbus' 'glib2' 'json-glib' 'steamos-atomupd-client' 'polkit' 'systemd')
provides=("${pkgname}")
conflicts=("${pkgname}")
source=("${pkgname}::git+https://github.com/evlav/atomupd-daemon#tag=v${pkgver}")
sha256sums=('2ff9ade7414f3b151a302379cdad14ac0a3278ee76e2fe04a5e01e31aed9ab3a')

build() {
  arch-meson "${pkgname}" build
  meson compile -C build
}

check() {
  export DBUS_SESSION_BUS_ADDRESS=`dbus-daemon --fork --config-file=/usr/share/dbus-1/session.conf --print-address`
  # A new system bus is needed for the mock polkit
  export DBUS_SYSTEM_BUS_ADDRESS=`dbus-daemon --fork --config-file="${pkgname}"/ci/dbus_system.conf --print-address`

  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -d -o root -g 102 -m 750 "${pkgdir}"/usr/share/polkit-1/rules.d
}

